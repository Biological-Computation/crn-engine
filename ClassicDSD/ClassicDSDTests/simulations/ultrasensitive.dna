directive simulation { final  = 500000.0
					 ; points = 1000
					 ; plots  = [<_ tl1p^ l1p>; <_ tl2p^ l2p>; <_ tl3p^ l3p>] }
directive deterministic {stiff = true; reltolerance = 0.00001}
def bind = 0.00003 (* /nM/s *) 
def unbind = 0.1126 (* /s *)
def Buff = 10000
new tstim@bind,unbind
new tl1@bind,unbind new tl1p@bind,unbind
new tl2@bind,unbind new tl2p@bind,unbind
new tl3@bind,unbind new tl3p@bind,unbind

def BTr(M,N, tx,x, ty,y) =
new ta@bind,unbind new a new b new tB@bind,unbind new B
( M of {tB^*}[B tx^]:[x ta^]:[a ty^]<y>:[b tB^]<B>
| M of <ta^ a ty^ b tB^>
| M of [x]{ta^*}
| M of [B]{tx^*}
| N of <b tB^ B>
)

def BF2(M,N, tx,x, ty,y, tz,z) =
new ta@bind,unbind new a new b new c new tB@bind,unbind new B
( M of {tB^*}[B tx^]:[x ta^]:[a ty^]<y>:[b tz^]<z>:[c tB^]<B>
| M of <ta^ a ty^ b tz^ c tB^>
| M of [x]{ta^*}
| M of [B]{tx^*}
| N of <c tB^ B>
)

def BJ2x2(M,N, tx,x, ty,y, tz,z, tw,w) =
new ta@bind,unbind new a new b new c
new td@bind,unbind new d new tB@bind,unbind new B
( M of {tB^*}[B tx^]:[x ty^]:[y ta^]:[a tz^]<z>:[b tw^]<w>:[c tB^]<B>
| M of <ta^ a tz^ b tw^ c tB^>
| M of [B]{tx^*}
| M of [x]:[ty^ d]:[td^ y]{ta^*}
| M of <d td^>
| M of {td^*}[y]
| M of {ty^*}[d]
| N of <c tB^ B>
)

( BF2(Buff,10, tstim,stim, tl1,l1, tstim,stim)
| BTr(Buff,10, tl1,l1, tl1p,l1p)
| BJ2x2(Buff,10, tl1p,l1p, tl2,l2, tl1,l1, tl2p,l2p)
| BJ2x2(Buff,10, tl2p,l2p, tl3,l3, tl2,l2, tl3p,l3p)
| BTr(Buff,1, tl3p,l3p, tl3,l3)
| 1000 of <hl2 tl2^ l2>
| 1000 of <hl3 tl3^ l3>
| 10 of <hstim tstim^ stim>
)